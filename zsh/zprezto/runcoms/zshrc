
# startup speed benchmarks
# https://htr3n.github.io/2018/07/faster-zsh/
# uncomment the line below, then source .zshrc and run zprof.
#zmodload zsh/zprof

# Fix for dirty escape sequences when TUI apps exit (lazygit, etc.)
# Disable ZLE enhanced keyboard protocol to prevent escape sequences
# from being echoed when TUI applications exit
# This stops ^[[81;16;113;0;0;1_% sequences from appearing
if [[ -o interactive ]]; then
  stty -echo 2>/dev/null
  while read -t 0.001 -k 1 __discard 2>/dev/null; do :; done
  stty echo 2>/dev/null
  unset __discard
fi

# Load custom/env specific stuff that needs to be loaded BEFORE Powerlevel10k.
# This is needed to enable Powerlevel10k instant prompt. Do this only if file exists.
if [[ -a $HOME/.zsh_custom_pre ]]; then
  source $HOME/.zsh_custom_pre
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#
# Executes commands at the start of an interactive session.
#

# Add custom completions to fpath BEFORE prezto loads so its compinit picks them up.
# This avoids a second compinit call later.
if [[ -d $HOME/dotfiles/zsh/completions ]]; then
  fpath=($HOME/dotfiles/zsh/completions $fpath)
fi

# Source Prezto (handles compinit via its completion module).
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

#
# Plugins (zplug-managed: forgit, git-cal)
#
export ZPLUG_HOME=$HOME/.zplug

if [[ -d "$ZPLUG_HOME" ]]; then
  # zplug bin dir in PATH immediately (git-cal and other command plugins live here)
  [[ -d "$ZPLUG_HOME/bin" ]] && export PATH="$ZPLUG_HOME/bin:$PATH"

  # Source plugin files directly before first prompt via a one-shot precmd hook.
  # Deliberately avoids calling `zplug 'user/repo'` declarations here because
  # those always print "[zplug] X: already managed" to stdout, which p10k
  # instant prompt detects as console output during initialization â†’ warning.
  # Plugin files are sourced straight from their installed repo paths instead.
  # On a fresh machine run `zplug_init` once to install, then this works.
  _zplug_load_once() {
    add-zsh-hook -d precmd _zplug_load_once
    [[ -f "$ZPLUG_HOME/repos/wfxr/forgit/forgit.plugin.zsh" ]] && \
      source "$ZPLUG_HOME/repos/wfxr/forgit/forgit.plugin.zsh"
  }
  add-zsh-hook precmd _zplug_load_once

  # zplug CLI - lazy-load on first use for management (update, status, etc.)
  zplug() {
    unfunction zplug
    source "$ZPLUG_HOME/init.zsh"
    zplug "$@"
  }
fi

# Install or update all zplug plugins. Run this on a fresh machine or to update.
# When adding a new plugin: add it here AND add a source line in _zplug_load_once.
zplug_init() {
  unfunction zplug 2>/dev/null
  source "$ZPLUG_HOME/init.zsh"
  zplug 'zplug/zplug', hook-build:'zplug --self-manage'
  zplug 'wfxr/forgit'
  zplug "k4rthik/git-cal", as:command, frozen:1
  zplug install
  zplug load
}

#
# Prompt and theme
#

# overrides theme set in .zpreztorc
prompt powerlevel10k

# enable or disable custom themes on the fly
CUSTOM_THEMES_ENABLED=0

# load custom themes
if [[ CUSTOM_THEMES_ENABLED -eq 1 && -d $HOME/.zsh_user_themes ]]; then
  # file namings must be: prompt_THEMENAME_setup
  fpath=( $HOME/.zsh_user_themes $fpath )
  autoload -Uz promptinit; promptinit
  # set a custom theme here. This overrides .zpreztorc theme setting.
  prompt sorinEdit
fi

#
# Options
#

# custom history size
export HISTSIZE=20000
# save history after logout
export SAVEHIST=20000
# history file
export HISTFILE=~/.zsh_history

#
# Load modules
#

# zmv is already loaded via zpreztorc: zstyle ':prezto:load' zfunction 'zargs' 'zmv'

# bash autocompletion
autoload -Uz bashcompinit
bashcompinit
for file in $HOME/dotfiles/bash/completions/*(.); source $file

# load interactive env
source $HOME/dotfiles/zsh/common/zsh_env_interactive

# load aliases
source $HOME/dotfiles/zsh/common/zsh_aliases

# lazy-load functions
fpath=($fpath ~/dotfiles/zsh/autoloaded)
for file in ~/dotfiles/zsh/autoloaded/*(N:t); do
  autoload -Uz $file
done

# load custom/env specific stuff, but only if file exists
if [[ -a $HOME/.zsh_custom ]]; then
  source $HOME/.zsh_custom
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# aws-cli autocomplete
if [[ -n $(command -v aws_completer) ]]; then
  complete -C 'aws_completer' aws
fi

# tabtab source for packages
# uninstall by removing these lines
[[ -f ~/.config/tabtab/__tabtab.zsh ]] && . ~/.config/tabtab/__tabtab.zsh || true

# Fix for dirty escape sequences after TUI apps (lazygit, etc.) exit
# This prevents keyboard protocol query responses from being echoed
function _fix_terminal_quirks() {
  # Consume any lingering escape sequences from stdin
  while read -t 0.001 -k 1 2>/dev/null; do :; done
}
# Run after every command to clean up terminal state
autoload -Uz add-zsh-hook
add-zsh-hook precmd _fix_terminal_quirks

# load function to run every time to cd into a directory
source "$HOME/dotfiles/zsh/common/chpwd.zsh"
# this runs every time to cd into a directory
function chpwd {
  # things to do each time you change directory. Zsh will call this.
  _chpwd_run_all
}

# Defer first-run chpwd to before the first prompt draw (not during rc parsing).
# This prevents the P10k "[WARNING]: Console output during zsh initialization
# detected." warning caused by prompt_oscseq emitting OSC sequences during init.
_defer_chpwd_startup() {
  _chpwd_run_all
  add-zsh-hook -d precmd _defer_chpwd_startup
}
add-zsh-hook precmd _defer_chpwd_startup

# quickly start zsh with p10k instant prompt, but avoid prompt flickering
(( ! ${+functions[p10k]} )) || p10k finalize
